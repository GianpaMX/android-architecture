apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'jacoco'

def SDK_VERSION = 27

def BUILD_TOOLS_VERSION = "27.0.2"

android {
    compileSdkVersion SDK_VERSION
    buildToolsVersion BUILD_TOOLS_VERSION
    defaultConfig {
        applicationId "io.github.gianpamx.androidarchitecture"
        minSdkVersion 16
        targetSdkVersion SDK_VERSION
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "io.github.gianpamx.android.architecture.CustomTestRunner"
    }
    buildTypes {
        debug {
            testCoverageEnabled !project.hasProperty('android.injected.invoked.from.ide')
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

jacoco {
    toolVersion = "0.7.4+"
}

dependencies {
    def DAGGER_VERSION = '2.14.1'
    def ARCH_COMP_VERSION = '1.0.0'
    def MOCKITO_KOTLIN_VERSION = '1.5.0'
    def GLIDE_VERSION = '4.5.0'
    def RETROFIT_VERSION = '2.3.0'

    kapt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    implementation "com.google.dagger:dagger:$DAGGER_VERSION"
    implementation "com.google.dagger:dagger-android:$DAGGER_VERSION"
    implementation "com.google.dagger:dagger-android-support:$DAGGER_VERSION"
    kapt "com.google.dagger:dagger-android-processor:$DAGGER_VERSION"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "com.android.support:appcompat-v7:$BUILD_TOOLS_VERSION"
    implementation "com.android.support:design:$BUILD_TOOLS_VERSION"

    implementation "android.arch.lifecycle:extensions:$ARCH_COMP_VERSION"
    kapt "android.arch.lifecycle:compiler:$ARCH_COMP_VERSION"

    implementation "android.arch.persistence.room:runtime:$ARCH_COMP_VERSION"
    kapt "android.arch.persistence.room:compiler:$ARCH_COMP_VERSION"

    implementation "com.github.bumptech.glide:glide:$GLIDE_VERSION"
    kapt "com.github.bumptech.glide:compiler:$GLIDE_VERSION"

    implementation "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
    implementation "com.squareup.retrofit2:converter-gson:$RETROFIT_VERSION"

    testImplementation "junit:junit:4.12"
    testImplementation "android.arch.core:core-testing:$ARCH_COMP_VERSION"
    testImplementation "android.arch.persistence.room:testing:$ARCH_COMP_VERSION"
    testImplementation "com.nhaarman:mockito-kotlin:$MOCKITO_KOTLIN_VERSION"
    testImplementation "com.squareup.retrofit2:retrofit-mock:$RETROFIT_VERSION"


    androidTestImplementation 'org.mockito:mockito-android:2.13.0'
    androidTestImplementation "com.nhaarman:mockito-kotlin:$MOCKITO_KOTLIN_VERSION"
    androidTestImplementation "com.android.support.test:runner:1.0.1"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.1"
    androidTestImplementation "android.arch.core:core-testing:1.0.0"
    kaptAndroidTest "com.google.dagger:dagger-android-processor:$DAGGER_VERSION"
}

task fullCoverageReport(type: JacocoReport) {
    dependsOn 'createDebugCoverageReport'
    dependsOn 'testDebugUnitTest'
    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*',
                      '**/*Test*.*', 'android/**/*.*',
                      '**/*_MembersInjector.class',
                      '**/Dagger*Component.class',
                      '**/Dagger*Component$Builder.class',
                      '**/*_*Factory.class',
                      '**/*ComponentImpl.class',
                      '**/*SubComponentBuilder.class']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def kotlinDebugTree = fileTree(dir: "${buildDir}/tmp/kotlin-classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree, kotlinDebugTree])
    executionData = fileTree(dir: "$buildDir", includes: [
            "jacoco/testDebugUnitTest.exec",
            "outputs/code-coverage/connected/*coverage.ec"
    ])
}
