apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'jacoco'

def SDK_VERSION = 27

android {
    compileSdkVersion SDK_VERSION

    defaultConfig {
        applicationId "io.github.gianpamx.androidarchitecture"
        minSdkVersion 16
        targetSdkVersion SDK_VERSION
        versionCode 1
        versionName "1.0"
        testInstrumentationRunner "io.github.gianpamx.android.architecture.CustomTestRunner"
    }
    buildTypes {
        debug {
            testCoverageEnabled !project.hasProperty('android.injected.invoked.from.ide')
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

jacoco {
    toolVersion = "0.8.1"
}

tasks.withType(Test) {
    jacoco.includeNoLocationClasses = true
}

dependencies {
    def DAGGER_VERSION = '2.14.1'
    def ARCH_COMP_VERSION = '1.1.1'
    def ROOM_VERSION = '1.0.0'
    def MOCKITO_KOTLIN_VERSION = '1.5.0'
    def GLIDE_VERSION = '4.6.1'
    def RETROFIT_VERSION = '2.3.0'
    def BUILD_TOOLS_VERSION = "27.1.1"

    kapt "com.google.dagger:dagger-compiler:$DAGGER_VERSION"
    implementation "com.google.dagger:dagger:$DAGGER_VERSION"
    implementation "com.google.dagger:dagger-android:$DAGGER_VERSION"
    implementation "com.google.dagger:dagger-android-support:$DAGGER_VERSION"
    kapt "com.google.dagger:dagger-android-processor:$DAGGER_VERSION"

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"
    implementation "com.android.support:appcompat-v7:$BUILD_TOOLS_VERSION"
    implementation "com.android.support:design:$BUILD_TOOLS_VERSION"

    implementation "android.arch.lifecycle:extensions:$ARCH_COMP_VERSION"
    kapt "android.arch.lifecycle:compiler:$ARCH_COMP_VERSION"

    implementation "android.arch.persistence.room:runtime:$ROOM_VERSION"
    kapt "android.arch.persistence.room:compiler:$ROOM_VERSION"

    implementation "com.github.bumptech.glide:glide:$GLIDE_VERSION"
    kapt "com.github.bumptech.glide:compiler:$GLIDE_VERSION"

    implementation "com.squareup.retrofit2:retrofit:$RETROFIT_VERSION"
    implementation "com.squareup.retrofit2:converter-gson:$RETROFIT_VERSION"

    testImplementation "junit:junit:4.12"
    testImplementation "android.arch.core:core-testing:$ARCH_COMP_VERSION"
    testImplementation "android.arch.persistence.room:testing:$ROOM_VERSION"
    testImplementation "com.nhaarman:mockito-kotlin:$MOCKITO_KOTLIN_VERSION"
    testImplementation "com.squareup.retrofit2:retrofit-mock:$RETROFIT_VERSION"
    testImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"


    androidTestImplementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    androidTestImplementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    androidTestImplementation 'org.mockito:mockito-android:2.13.0'
    androidTestImplementation "com.nhaarman:mockito-kotlin:$MOCKITO_KOTLIN_VERSION"
    androidTestImplementation "com.android.support.test:runner:1.0.1"
    androidTestImplementation "com.android.support.test.espresso:espresso-core:3.0.1"
    androidTestImplementation "android.arch.core:core-testing:$ARCH_COMP_VERSION"
    androidTestImplementation('com.schibsted.spain:barista:2.3.0') {
        exclude group: 'com.android.support'
        exclude group: 'org.jetbrains.kotlin'
    }
    kaptAndroidTest "com.google.dagger:dagger-android-processor:$DAGGER_VERSION"
}

task fullCoverageReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {
    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = [
            '**/*Test*.*',
            '**/AutoValue_*.*',
            '**/*JavascriptBridge.class',
            '**/R.class',
            '**/R$*.class',
            '**/Manifest*.*',
            'android/**/*.*',
            '**/BuildConfig.*',
            '**/*$ViewBinder*.*',
            '**/*$ViewInjector*.*',
            '**/Lambda$*.class',
            '**/Lambda.class',
            '**/*Lambda.class',
            '**/*Lambda*.class',
            '**/*$InjectAdapter.class',
            '**/*$ModuleAdapter.class',
            '**/*$ViewInjector*.class',
            '**/*_Impl*',
            '**/Dagger*Component*',
            '**/Dagger*Impl*',
            '**/Dagger*Builder*',
            '**/*_MembersInjector.class', //Dagger2 generated code
            '*/*_MembersInjector*.*', //Dagger2 generated code
            '**/*_*Factory*.*', //Dagger2 generated code
            '*/*Component*.*', //Dagger2 generated code
            '**/*Module*.*' //Dagger2 generated code
    ]
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def kotlinDebugTree = fileTree(dir: "${buildDir}/tmp/kotlin-classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree, kotlinDebugTree])
    executionData = fileTree(dir: "$buildDir", includes: [
            "outputs/code-coverage/connected/*coverage.ec",
            "jacoco/testDebugUnitTest.exec"
    ])
}
